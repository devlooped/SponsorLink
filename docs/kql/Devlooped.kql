let toquery = (uri: string) {
    let query = parse_url(uri)["Query Parameters"];
    iif(tostring(query["account"]) contains "%26", 
        parse_urlquery(url_decode(strcat("account=", query["account"])))["Query Parameters"], 
        query);
};
StorageBlobLogs
| where TimeGenerated > ago(6d)
| where OperationName == "GetBlobProperties" or OperationName == "GetBlob"
| extend Parts=split(ObjectKey, '/')
| extend Project=tostring(Parts[2]), Container=tostring(Parts[3]), Email=tostring(Parts[4])
| where Project == "sponsorlink" and Container == "apps"
| extend Query=toquery(Uri)
| extend Account=tostring(Query["account"]), 
         Product=tostring(Query["product"]), 
         Package=tostring(Query["package"]), 
         Version=tostring(Query["version"])
| project TimeGenerated, Email, 
          Account=iif(isempty(Account), "devlooped", Account),
          Product=iif(isempty(Product), "unknown", Product),
          Package=iif(isempty(Package), iif(isempty(Product), "unknown", Product), Package),
          Version
| summarize Count=count() by Account, Product, Package, Version, Email